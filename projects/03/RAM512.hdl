// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM512.hdl
/**
 * Memory of 512 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    
    // select to which ram to propagate load bit to.
    DMux8Way(
        in=load,   sel=address[6..8],
        a=ramsel0, b=ramsel1,
        c=ramsel2, d=ramsel3,
        e=ramsel4, f=ramsel5,
        g=ramsel6, h=ramsel7
    );

    // create ram blocks
    RAM64(in=in, load=ramsel0, address=address[0..5], out=ram0);
    RAM64(in=in, load=ramsel1, address=address[0..5], out=ram1);
    RAM64(in=in, load=ramsel2, address=address[0..5], out=ram2);
    RAM64(in=in, load=ramsel3, address=address[0..5], out=ram3);
    RAM64(in=in, load=ramsel4, address=address[0..5], out=ram4);
    RAM64(in=in, load=ramsel5, address=address[0..5], out=ram5);
    RAM64(in=in, load=ramsel6, address=address[0..5], out=ram6);
    RAM64(in=in, load=ramsel7, address=address[0..5], out=ram7);

    // select which ram output to propagate
    Mux8Way16(
        a=ram0, b=ram1, 
        c=ram2, d=ram3, 
        e=ram4, f=ram5, 
        g=ram6, h=ram7, 
        sel=address[6..8], out=out
    );
}